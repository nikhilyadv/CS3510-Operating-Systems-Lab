[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 9;
alias userSP R1;
userSP=SP;
SP = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 11]*512 - 1;
alias filename R3;
filename = [[PTBR + 2 * ((userSP - 4)/ 512)] * 512 + (userSP - 4) % 512];
alias fileindex R4;
alias flag R5;
flag = 0;
fileindex = 0;				//inode table page 59
while (fileindex < 960) do
	if ([INODE_TABLE + fileindex+1] == filename) then
		if ([INODE_TABLE + fileindex] == EXEC) then
			flag = 1;
			break;
		endif;
	endif;
	fileindex = fileindex + 16;
endwhile;

if (flag == 0) then
	lb:
		alias physicalAddrRetVal R10;
		physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
		[physicalAddrRetVal] = -1;
		SP = userSP;
else
	fileindex = (fileindex)/16;
	multipush(R1,R3,R4,R5);
	R1 = 4;											//acquire inode
	R2 = (fileindex);
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R3,R4,R5);
	if(R0 == -1) then
		goto lb;
	endif;
	alias t R13;
	t = FILE_STATUS_TABLE + 4*fileindex + 1;
	if ([t] == -1) then
		[t] = 1;
	else
		[t] = [t] + 1;
	endif;
	multipush(R1,R3,R4,R5);
	R1 = 5;											//release inode
	R2 = fileindex;
	R3 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_0;
	multipop(R1,R3,R4,R5);
	fileindex = (INODE_TABLE + fileindex*16);
	multipush(R1,R2,R3,R4,R5);
	R1 = 3; 							//calling exit
	R2 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_1;
	multipop(R1,R2,R3,R4,R5);
	alias UserAreaPage R6;
	UserAreaPage = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11];
	[SYSTEM_STATUS_TABLE + 2] = [SYSTEM_STATUS_TABLE +2 ] - 1;
	[MEMORY_FREE_LIST + UserAreaPage] = [MEMORY_FREE_LIST + UserAreaPage] + 1;
	SP = UserAreaPage*512 - 1;
	[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 4] = RUNNING;
	[PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 7] = ((fileindex - INODE_TABLE)/16);
	alias ptbr R7;
	ptbr = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 14];
	[ptbr+0] = 63;
	[ptbr+1] = "0100";
	[MEMORY_FREE_LIST + 63] = [MEMORY_FREE_LIST + 63] + 1;
	[ptbr+2] = 64;
	[ptbr+3] = "0100";
	[MEMORY_FREE_LIST + 64] = [MEMORY_FREE_LIST + 64] + 1;
	[ptbr+5] = "0000";		//invalidate heap pages
	[ptbr+7] = "0000";
	alias counter R8;
	counter = 0;
	while (counter < 2) do			//Stack
		multipush(R1,R2,R3,R4,R5,R6,R7,R8);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8);
		[ptbr + 16 + counter*2] = R0;
		[ptbr + 16 + counter*2 + 1] = "0110";
		counter = counter + 1;
	endwhile;
	if([fileindex + 8] != -1) then				//first code page
		multipush(R3,R4,R5,R6,R7,R8);
		R1 = 5;									//get code page
		R2 = [fileindex + 8];
		call MOD_2;
		multipop(R3,R4,R5,R6,R7,R8);
		[ptbr+8] = R0;
		if (R0 > 0) then
			[ptbr+9] = "0100";
		else
			[ptbr+9] = "0000";
		endif;
	endif;
	[ptbr + 11] = "0000";		//invalidate
	[ptbr + 13] = "0000";
	[ptbr + 15] = "0000";
	counter = 0;				//Disk Map table
	while(counter < 10) do
		[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + counter] = -1;
		counter = counter + 1;
	endwhile;
	[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 4] = [fileindex + 8];
	[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 5] = [fileindex + 9];
	[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 6] = [fileindex + 10];
	[DISK_MAP_TABLE + [SYSTEM_STATUS_TABLE + 1]*10 + 7] = [fileindex + 11]; 
	//preprocess table
	UserAreaPage = (UserAreaPage) * 512 + (512 - 16);		//semaphores
	counter = 0;
	while(counter < 16) do
		[UserAreaPage+counter] = -1;
		counter = counter + 1;
	endwhile;
	[[ptbr + 16]*512]=[[ptbr + 8]*512+1];
	SP = 8*512;
endif;
[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
//print("Exec");
//breakpoint;
ireturn;