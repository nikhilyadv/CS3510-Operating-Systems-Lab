[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 9;
alias userSP R0;
userSP=SP;
SP = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 11]*512 - 1;
alias physicalPageNum R1;
alias offset R2;
alias filenameAddr R3;
physicalPageNum = [PTBR + 2 * ((userSP - 4)/ 512)];
offset = (userSP - 4) % 512;
filenameAddr = (physicalPageNum * 512) + offset;
alias filename R4;
filename=[filenameAddr];
alias index R5;
alias flag R6;
flag = 0;
index = 0;				//inode table page 59
while (index < 960) do
	if ([INODE_TABLE + index+1] == filename) then
		if ([INODE_TABLE + index] == EXEC) then
			flag = 1;
			break;
		endif;
	endif;
	index = index + 16;
endwhile;
if (flag == 0) then
	alias physicalAddrRetVal R7;
	physicalAddrRetVal = ([PTBR + 2 * ((userSP - 1) / 512)] * 512) + ((userSP - 1) % 512);
	[physicalAddrRetVal] = -1;
else
	alias fileindex R7;
	fileindex = (INODE_TABLE + index);
	multipush(R1,R2,R3,R4,R5,R6,R7);
	R1 = 3; //calling exit
	R2 = [SYSTEM_STATUS_TABLE + 1];
	call MOD_1;
	multipop(R1,R2,R3,R4,R5,R6,R7);
	alias UserAreaPage R8;
	UserAreaPage = [PROCESS_TABLE + ([SYSTEM_STATUS_TABLE + 1] * 16) + 11];
	[SYSTEM_STATUS_TABLE+2] = [SYSTEM_STATUS_TABLE+2] - 1;
	[MEMORY_FREE_LIST + UserAreaPage] = [MEMORY_FREE_LIST + UserAreaPage] + 1;
	SP = UserAreaPage*512 - 1;
	[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 4] = RUNNING;
	[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 7] = fileindex;
	alias ptbr R8;
	ptbr = [PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 14];
	[ptbr+0] = 63;
	[ptbr+1] = "0100";
	[ptbr+2] = 64;
	[ptbr+3] = "0100";
	alias counter R9;
	counter = 0;
	while (counter < 2) do			//Heap
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 4 + counter*2] = R0;
		[ptbr + 4 + counter*2 + 1] = "0110";
		counter = counter + 1;
	endwhile;
	counter = 0;
	while (counter < 2) do			//Stack
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 16 + counter*2] = R0;
		[ptbr + 16 + counter*2 + 1] = "0110";
		counter = counter + 1;
	endwhile;
	if([fileindex + 8] != -1) then
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 8] = R0;
		[ptbr + 9] = "0100";
		loadi(R0,[fileindex + 8]);
	endif;
	if([fileindex + 9] != -1) then
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 10] = R0;
		[ptbr + 11] = "0100";
		loadi(R0,[fileindex + 9]);
	endif;
	if([fileindex + 10] != -1) then
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 12] = R0;
		[ptbr + 13] = "0100";
		loadi(R0,[fileindex + 10]);
	endif;
	if([fileindex + 11] != -1) then
		multipush(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		R1 = 1;
		call MOD_2;
		multipop(R1,R2,R3,R4,R5,R6,R7,R8,R9);
		[ptbr + 14] = R0;
		[ptbr + 15] = "0100";
		loadi(R0,[fileindex + 11]);
	endif;
	[[ptbr + 16]*512]=[[ptbr + 8]*512+1];
	SP = 8*512;
	[PROCESS_TABLE + ( [SYSTEM_STATUS_TABLE + 1] * 16) + 9] = 0;
endif;
ireturn;